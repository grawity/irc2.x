dnl
dnl --------------------------------------------------------------------------
dnl                   IRC m4 extended macros (ircd.m4ext)
dnl --------------------------------------------------------------------------
dnl            Version 2.0, internal revision number 19931105 --Nap
dnl            $Id: ircd.m4ext,v 2.0 1993/11/05 21:21:18 pioch Exp $
dnl
dnl --- SERVER INFORMATION
dnl
M:SERVERNAME::SERVERTEXT:SERVERPORT
A:ADMINLOCATION:ADMINISTRATOR:ADMIN_EMAIL
dnl
dnl --- CONNECTION CLASSES
dnl
dnl NOTE: You can edit this section, but default values should be okay.
dnl 
dnl    PING_SERVER : server ping frequency in seconds
dnl    PING_CLIENT : client ping frequency in seconds
dnl
define(`PING_SERVER',120)dnl
define(`PING_CLIENT',300)dnl
dnl
dnl --- Client Classes.
dnl
dnl NOTE: You can edit this section, but default values should be okay.
dnl
dnl Two type of clients are defined:
dnl 'CLIENT' are supposed to be local
dnl 'GUESTS' are remote users which have restrictions.
dnl
dnl    CLIENT_MAX   : is the maximum number of clients you want connected.
dnl    CLIENT_CLASS : we'll put clients in class 1, no change needed.
dnl    CLIENT_SENDQ : limit in SendQ for clients, in bytes.
dnl
define(`CLIENT_MAX',   `100')dnl
define(`CLIENT_CLASS', `1')dnl
define(`CLIENT_SENDQ', `60000')dnl
dnl
define(`GUEST_MAX',   `50')dnl
define(`GUEST_CLASS', `2')dnl
define(`GUEST_SENDQ', `40000')dnl
dnl
dnl --- Server Class.
dnl    DOWNLINK_CLASS : we'll put downlinks in class 5, no change needed.
dnl    AUTOCON_CLASS  : we'll put autoconnect in class 8, no change needed.
dnl    UPLINK_CLASS   : we'll put uplinks in class 9, no change needed.
dnl
define(`DOWNLINK_CLASS',5)dnl
define(`AUTOCON_CLASS',8)dnl
define(`UPLINK_CLASS',9)dnl
dnl
dnl --- Definition of classes
dnl
Y:CLIENT_CLASS:PING_CLIENT:0:CLIENT_MAX:CLIENT_SENDQ
Y:GUEST_CLASS:PING_CLIENT:0:GUEST_MAX:GUEST_SENDQ
Y:DOWNLINK_CLASS:PING_SERVER:0:SERVER_MAXCONNECT:SERVER_SENDQ
Y:AUTOCON_CLASS:PING_SERVER:AUTOCON_CONNECT:SERVER_MAXCONNECT:SERVER_SENDQ
Y:UPLINK_CLASS:PING_SERVER:UPLINK_CONNECT:1:SERVER_SENDQ
dnl
dnl --- AUTORIZED CLIENTS
dnl
define(`IDENT',`ifelse(len($1),0,,*@$1)')dnl
define(`CLIENT_DOAUTH',dnl
I:`IDENT($1)':$3:`IDENT(ifelse(len($2),0,$1,$2))'::CLIENT_CLASS)dnl
define(`CLIENT_NOAUTH',I:$1:$3:`ifelse(len($2),0,$1,$2)'::CLIENT_CLASS)dnl
dnl
define(`GUEST_DOAUTH',dnl
I:`IDENT($1)':$3:`IDENT(ifelse(len($2),0,$1,$2))'::GUEST_CLASS)dnl
define(`GUEST_NOAUTH',I:$1:$3:`ifelse(len($2),0,$1,$2)'::GUEST_CLASS)dnl
dnl
dnl --- AUTORIZED OPERATORS
dnl
define(`OPERATOR',O:$2:$3:$1::CLIENT_CLASS)dnl
dnl
dnl --- KILLED USERS
dnl
define(`CLIENT_KILLED',K:$1::`ifelse(len($2),0,*,$2)')dnl
dnl
dnl --- LISTEN TO ADDITIONNAL PORTS
dnl
define(`ADD_PORT',P:`ifelse(len($2),0,*,$2)':::$1)dnl
dnl
dnl --- SERVER CONNECTIONS
dnl
define(`SERVER_DOWNLINK',`dnl
C:$1:$2:ifelse(len($4),0,$1,$4)::DOWNLINK_CLASS
N:$1:ifelse(len($3),0,$2,$3):ifelse(len($4),0,$1,$4):$5:DOWNLINK_CLASS')dnl
dnl
define(`SERVER_AUTOCON',`dnl
C:$1:$2:ifelse(len($4),0,$1,$4):ifelse(len($5),0,6667,$5):AUTOCON_CLASS
N:$1:ifelse(len($3),0,$2,$3):ifelse(len($4),0,$1,$4):$6:AUTOCON_CLASS')dnl
dnl
define(`SERVER_UPLINK',`dnl
C:$1:$2:ifelse(len($4),0,$1,$4):ifelse(len($5),0,6667,$5):UPLINK_CLASS
N:$1:ifelse(len($3),0,$2,$3):ifelse(len($4),0,$1,$4):$6:UPLINK_CLASS')dnl
dnl
define(`SERVER_LEAF',L:$2::$1:$3)dnl
define(`SERVER_HUB',H:`ifelse(len($2),0,*,$2)':*:$1)dnl
dnl
